plugins {
    // To upload the Artifact to Maven Central
    // See: https://docs.gradle.org/current/userguide/publishing_maven.html
    id 'maven-publish'
    id 'signing'
}

group = rootProject.group
version = rootProject.version

// Java 8 is the only Java version supported
sourceCompatibility = JavaVersion.VERSION_1_8
ext {
    javaVersion = JavaVersion.current()
    ossrhUsername = System.getenv("OSSRH_USERNAME") ?: properties['ossrhUsername']
    ossrhPassword = System.getenv("OSSRH_PASSWORD") ?: properties['ossrhPassword']
}

if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    throw new GradleException(
            "This build must be run with Java 8, using instead version ${javaVersion}")
}

repositories {
    mavenLocal()
    mavenCentral()
}

// To emit the details of the error for the "uses or overrides a deprecated API" error.
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:deprecation"
        options.compilerArgs << "-Xlint:unchecked"
    }
}

javadoc {
    title = 'Analyse SDK'
    failOnError = true

    destinationDir = file("${rootDir}/docs/javadoc/${project.name}/${version}")
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

// Generating Source & Javadoc JARs for publication
// See: https://central.sonatype.org/publish/publish-gradle/
java {
    withJavadocJar()
    withSourcesJar()
}

jar.enabled = true
jar.dependsOn shadowJar

shadowJar {
    archiveClassifier.set('')
}

// Publishing Artifact to Maven Central
//
// The contents of this section are described here:
//   https://docs.gradle.org/current/userguide/publishing_maven.html
//
// To Release to Maven see: https://central.sonatype.org/publish/release/
publishing {
    publications {
        sdk(MavenPublication) {
            artifactId = 'sdk'
            artifacts = [ jar, javadocJar, sourcesJar ]
            pom {
                name = 'Analyse SDK'
                description = 'The official Java SDK for Analyse.'
                url = 'https://github.com/track/plugin'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/track/plugin/blob/main/LICENSE'
                    }
                }
                scm {
                    url = 'https://github.com/track/plugin'
                    connection = 'scm:https://github.com/track/plugin.git'
                    developerConnection = 'scm:git@github.com:track/plugin.git'
                }
                developers {
                    developer {
                        id = 'charliejoseph'
                        name = 'Charlie Joseph'
                        email = 'charlie.joseph@overwolf.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            // These are stored in gradle.properties and NEVER uploaded to GitHub
            // See: https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.repositories.MavenArtifactRepository.html
            credentials {
                username "${ossrhUsername}"
                password "${ossrhPassword}"
            }
        }
    }
}

// Signs the `publication` generated above with the name `sdk`
// Signing plugin, see: https://docs.gradle.org/current/userguide/signing_plugin.html#signing_plugin
signing {
    sign publishing.publications.sdk
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'dev.dejvokep:boosted-yaml:1.3'
    compileOnly 'com.google.code.gson:gson:2.10.1'
    compileOnly 'com.google.guava:guava:30.1.1-jre'
}